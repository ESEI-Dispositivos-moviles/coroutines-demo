plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.ksp)
}

android {
    namespace 'gal.uvigo.coroutines'
    compileSdk {
        version = release(36)
    }

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "gal.uvigo.coroutines"
        minSdk 36
        targetSdk 36
        versionCode 1
        versionName "1.0"
        def newsKey = project.findProperty("NEWSAPI_KEY") ?: System.getenv("NEWSAPI_KEY")
        if (!newsKey) {
            def lp = rootProject.file("local.properties")
            if (lp.exists()) {
                def props = new Properties()
                lp.withInputStream { stream -> props.load(stream) }
                newsKey = props.getProperty("NEWSAPI_KEY", "")
            } else {
                newsKey = ""
            }
        }
        buildConfigField("String", "NEWSAPI_KEY", "\"${newsKey}\"")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {

    // TODO 01. Add kotlinx-coroutines-core and kotlinx-coroutines-android to your version catalog (libs.versions.toml)

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.activity
    implementation libs.androidx.constraintlayout
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    implementation(libs.moshi)
    implementation(libs.moshi.kotlin)
    implementation(libs.retrofit)
    implementation(libs.converter.moshi)
    implementation(libs.okhttp)
    implementation(libs.logging.interceptor)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.lifecycle.viewmodel.ktx)
    implementation(libs.androidx.fragment.ktx)
    ksp(libs.moshi.kotlin.codegen)
}